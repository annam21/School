
  model {
    # priors
    b0.psi ~ dnorm( 0, 0.001 )T(-10,10)
    b0.p ~ dnorm( 0, 0.001 )T(-10,10)
    
    # For reporting only
    logit(mean.psi) <- b0.psi
    logit(mean.p) <- b0.p
    
    # For the model 
    for( i in 1:nSite ){
      logit(psi[i]) <- b0.psi
      for( t in 1:nOcc ){
        logit(p[i, t]) <- b0.p
      }
    }
    
    # likelihood
    for( i in 1:nSite ){
      # state
      mu1[i] <- psi[i]
      z[i] ~ dbern( mu1[i] )

      for( t in 1:nOcc ){
        # observation part 1
        mu2[i,t] <- p[i,t] * z[i]
      }
    }
    # Observation part 2
    for(j in 1:nObs){
      y[j] ~ dbern( mu2[site[j], occ[j]] )
    }

  }















  