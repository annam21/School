
  model {
    # priors
    b0.psi ~ dnorm( 0, 0.001 )T(-10, 10)
    b0.p ~ dnorm( 0, 0.001 )T(-10, 10)
    b0.theta ~ dnorm( 0, 0.001 )T(-10, 10)
    
    # For reporting only
    logit(mean.psi) <- b0.psi
    logit(mean.p) <- b0.p
    logit(mean.theta) <- b0.theta
    
    # For the model 
    for( i in 1:nSite ){
      logit(psi[i]) <- b0.psi
      
      for(k in 1:nCam){
        logit(theta[i, k]) <- b0.theta # Dimensions[site, cam]
      }      
    }
    for(j in 1:nObs){
      logit(p[site[j], time[j], space[j]]) <- b0.p
    }
    
    # Likelihood
    for( i in 1:nSite ){
      # State of site 
      mu1[i] <- psi[i]
      z[i] ~ dbern( mu1[i] )
      
      # State of camera (occ|plot occupied)
      for(k in 1:nCam){
        mu3[i, k] <- theta[i, k] * z[i]
        zz[i, k] ~ dbern(mu3[i, k])
      }
    }
    
    for(j in 1:nObs){
      # State of camera (occ|plot occupied)
#      mu3[site[j], space[j]] <- theta[site[j], space[j]] * z[site[j]]
#      zz[site[j], space[j]] ~ dbern(mu3[site[j], space[j]])
    
      # Observation (detected|cam occupied)
      mu2[site[j], time[j], space[j]] <- p[site[j], time[j], space[j]] * zz[site[j], space[j]]
      y[j] ~ dbern( mu2[site[j], time[j], space[j]] )
    }
}