
  model {
    # priors
    b0.psi ~ dnorm( 0, 0.001 )T(-10,10)
    b0.p ~ dnorm( 0, 0.001 )T(-10,10)
    b1.p ~ dnorm( 0, 0.001 )T(-10,10)
    for(i in 1:nSite){ 
        e[i] ~ dnorm(0, taue)
    }
    taue ~ dunif(0, 5)
    # Now even more priors
    muSize ~ dnorm(10, 0.001)
    tauSize ~ dunif(0, 10)
    
    # Covariate model 
    ### This is data
    for(i in 1:nSite){
        bodySize[i] ~ dnorm(muSize, tauSize)
    }
    
    logit(mean.psi) <- b0.psi
    logit(mean.p) <- b0.p
    
    for( i in 1:nSite ){
      psi[i] <- mean.psi
      for( t in 1:nOcc ){
        logit(p[i,t]) <- b0.p + b1.p*bodySize[i] + e[i] # Linear model
      }
    }
    
    # likelihood
    for( i in 1:nSite ){
      # state
      z[i] ~ dbern( mu1[i] )
      mu1[i] <- psi[i]
      for( t in 1:nOcc ){
        # observation
        y[i,t] ~ dbern( mu2[i,t] )
        mu2[i,t] <- p[i,t] * z[i]
      }
    }
    # Derived
    N <- sum(z)
  }
